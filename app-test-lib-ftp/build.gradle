buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url 'http://repo.enonic.com/public'
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath "com.enonic.xp:gradle-plugin:${xpVersion}"
        classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
    }
}

apply plugin: 'com.moowork.gulp'

//──────────────────────────────────────────────────────────────────────────────
// classname: AppPlugin
// defines: app, include, compile, runtime, build, jar, sourceSets
//──────────────────────────────────────────────────────────────────────────────
apply plugin: 'com.enonic.xp.app'

app {
    name = project.appName
    displayName = project.displayName
    vendorName = 'Enonic AS'
    vendorUrl = 'http://enonic.com'
}

repositories {
    mavenLocal()
    jcenter()
    maven {
        url 'http://repo.enonic.com/public'
    }
}

dependencies {
    include 'com.enonic.lib:util:1.1.3'
    include project(':lib-ftp') // NOTE: jar file must already be built.
} // dependencies

//──────────────────────────────────────────────────────────────────────────────
// Gulp
//──────────────────────────────────────────────────────────────────────────────
task installGulp(overwrite: true) {} // Skip gulp install...


task gulp_build(type: GulpTask, dependsOn: 'installGulp') {
    description = 'Build for production using gulp'
    args = ["--color", "--gulpfile", "gulpfile.babel.js", "prod"]
}


task gulp_watch(type: GulpTask, dependsOn: 'installGulp') {
    description = 'Build for development and watch using gulp'
    args = ["--color", "--gulpfile", "gulpfile.babel.js", "watch"]
}

gulp {
    // Set the directory where gulpfile.js should be found
    workDir = file("${project.projectDir}")
    // Whether colors should output on the terminal
    colors = true
}

// runs "gulp build" as part of your gradle build
build.dependsOn 'gulp_build'
build.mustRunAfter 'gulp_build'

//──────────────────────────────────────────────────────────────────────────────
// Let gulp handle everything except compiling java
//──────────────────────────────────────────────────────────────────────────────

tasks.withType(Copy) {
  includeEmptyDirs = false
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
            exclude '*'
        }
    }
}
