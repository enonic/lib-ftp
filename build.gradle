plugins {
	id 'java'
	id 'maven-publish'
    id 'com.enonic.xp.base' version '3.4.0'
	id "com.github.node-gradle.node" version '7.0.2'
}


repositories {
    xp.enonicRepo()
    mavenCentral()
}


dependencies {
    // Enonic core libs
    implementation "com.enonic.xp:lib-io:${xpVersion}"

    // Enonic libs
    implementation 'com.enonic.lib:lib-util:3.1.1'

    // Other libs
    implementation 'commons-net:commons-net:3.11.0'
} // dependencies


//──────────────────────────────────────────────────────────────────────────────
// Let gulp handle everything except compiling java
//──────────────────────────────────────────────────────────────────────────────

tasks.withType(Copy) {
  includeEmptyDirs = false
}

//──────────────────────────────────────────────────────────────────────────────
// Gradle node plugin
//──────────────────────────────────────────────────────────────────────────────
node {
	version "${nodeVersion}"
	download = true
}

//──────────────────────────────────────────────────────────────────────────────
// Webpack
//──────────────────────────────────────────────────────────────────────────────
sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
            exclude '*'
        }
    }
}

task webpack(type:NodeTask) {
	dependsOn yarn
	script = file('node_modules/webpack-cli/bin/cli.js')
	args = [
		'--color'
	]
	outputs.dir './build/resources/main' // processResources will delete the directory if this is not present
}


task watch(type:NodeTask) {
	script = file('node_modules/webpack-cli/bin/cli.js')
	args = [
		'--color',
		'--watch'
	]
}


processResources {
	dependsOn webpack

	exclude '**/.gitkeep'

	// Let webpack handle these (rather than gradle)
	exclude '**/*.css'
	exclude '**/*.es'
	exclude '**/*.js'
	exclude '**/*.jsx'
	exclude '**/*.less'
	exclude '**/*.sass'
	exclude '**/*.scss'
	exclude '**/*.styl'
}
